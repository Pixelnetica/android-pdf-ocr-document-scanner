apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
        ignoreExitValue = true
    }
    return stdout.toString().trim()
}

android {
    def gitHash
    try {
        gitHash = getGitHash()
    } catch (Exception w) {
        gitHash = ""
    }

    // Using Java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        demo {
            keyAlias 'com.pixelnetica.cropdemo'
            keyPassword '123456'
            storeFile file("${rootDir}/demo.keystore.jks")
            storePassword '123456'
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.pixelnetica.cropdemo"
        // NOTE: Don't change value here. Select build flavor
        buildConfigField "boolean", "DEVELOP", "false"
        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""

        minSdkVersion 19
        targetSdkVersion 28

        versionCode 37
        versionName "2.3.5." + versionCode.toString()

        ndk {
            abiFilters = ['armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86']
        }

    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
        }
        signed {
            initWith release
            matchingFallbacks = ['release']
            // To run release under IDE
            signingConfig signingConfigs.demo
        }

    }
    flavorDimensions "default"
    productFlavors {
        core {
            dimension "default"
        }
    }

    // Add build version to apk file name
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.flavorName}-${variant.versionCode}-${gitHash}.apk"
        }
    }
}

repositories{
    flatDir{
        dirs 'libs'
    }
    maven {
        url 'http://repo.pixelnetica.com:8081/artifactory/libs-release'
    }
}

dependencies {
    implementation('com.pixelnetica.sdk:scanning-release:2.3.5') { changing = true }
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:gridlayout-v7:28.0.0'
    implementation 'com.android.support:support-core-ui:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
}
