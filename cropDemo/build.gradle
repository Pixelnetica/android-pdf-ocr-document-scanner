apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
        ignoreExitValue = true
    }
    return stdout.toString().trim()
}

android {
    def gitHash
    try {
        gitHash = getGitHash()
    } catch (Exception w) {
        gitHash = ""
    }

    // Using Java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        demo {
            keyAlias 'com.pixelnetica.cropdemo'
            keyPassword '123456'
            storeFile file("${rootDir}/demo.keystore.jks")
            storePassword '123456'
        }
    }

    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "com.pixelnetica.cropdemo"
        // NOTE: Don't change value here. Select build flavor
        buildConfigField "boolean", "DEVELOP", "false"
        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""

        minSdkVersion 21
        targetSdkVersion 29

        versionCode 43
        versionName "2.3.7." + versionCode.toString()

        ndk {
            abiFilters = ['armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86']
        }

    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
        }
        signed {
            initWith release
            matchingFallbacks = ['release']
            // To run release under IDE
            signingConfig signingConfigs.demo
        }

    }

    // Add build version to apk file name
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${variant.flavorName}-v${variant.versionCode}-${gitHash}.apk"
        }
    }
}

repositories{
    flatDir{
        dirs 'libs'
    }
    maven {
        url 'http://repo.pixelnetica.com:8081/artifactory/libs-release'
    }
}

// Lookup dynamic libraries changes every 10 minutes
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
}

// Command to refresh dependencies
// gradlew.bat clean build --refresh-dependencies

dependencies {
    // Take release library "-SNAPSHOT" behaviour: update every changes, not only version number
    implementation('com.pixelnetica.sdk:scanning-release:2.3.7') { changing = true }

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
}
